//===== rAthena Script ======================================= 
//= Find the Mushroom
//===== By: ================================================== 
//= Mysterious
//===== Current Version: ===================================== 
//= 3.6a
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Find the Mushroom - random amount of Mushrooms spawns in random maps.
//= Players need to find these mushrooms and kill them to gain prizes!
//===== Additional Comments: =================================
//= 3.0 Fully Functional with Rewritten script. [Mysterious]
//= 3.6a Slightly edited. [Euphy]
//============================================================ 

prontera,188,209,3	script	Find the Mushroom	BLACK_MUSHROOM,{
	mes "[ Find The Mushroom ]";
	if (!.Status)
		mes "There is no event at the moment!";
	else {
		mes "There are "+.Spawn+" Mushrooms left in "+.map$+"!";
		mes "Find and kill the mushrooms to gain "+getitemname(.Prize)+"!";
	}
	if (.Status || getgmlevel() < .GM) close;
	mes "Start the event?";
	next;
	if(select("- No:- Yes") == 1) close;
	donpcevent strnpcinfo(0)+"::OnMinute10";
	mes "[ Find The Mushroom ]";
	mes "Event started!";
	close;

OnInit:
	set .Prize,7227;	// Reward item ID
	set .Amount,10;	// Reward item amount
	set .GM,99;	// GM level required to access NPC
	setarray .maps$[0],"izlude","geffen","morocc","prontera"; // Possible maps

	waitingroom "Mushroom Event",0;
	end;

OnClock0100:
OnClock0500:
OnClock0900:
OnClock1300:
OnClock1700:
OnClock2100:
	if (.Status) end;
	set .Status,1;
	set .Spawn,rand(1,2);	// How many Mushrooms should spawn?
	set .map$,.maps$[rand(getarraysize(.maps$))];
	killmonster .map$,"All";
	monster .map$,0,0,"Please don't kill me!",1084,.Spawn,strnpcinfo(0)+"::OnMobKilled";
	announce "Find the mushroom event will begin shortly!",0,0x00CCFF;
	sleep 2500;
	announce "Total of "+.Spawn+" Mushrooms have been spawned in "+.map$+"!",0,0x00CCFF;
	sleep 2500;
	announce "Every Mushroom you kill will give you "+getitemname(.Prize)+"!",0,0x00CCFF;
	end;

OnMobKilled:
	set .Spawn, .Spawn - 1;
	getitem .Prize, .Amount;
	if (.Spawn) announce "[ "+strcharinfo(0)+" ] has killed a Mushroom. There are now "+.Spawn+" Mushroom(s) left.",bc_map;
	else {
		announce "The Find the Mushroom Event has ended. All the Mushrooms have been killed.",0,0x00CCFF;
		set .Status,0;
	}
	end;
}
